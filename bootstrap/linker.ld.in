OUTPUT_FORMAT("elf32-i386")
OUTPUT_ARCH("i386")
ENTRY(_start)

#define KERNEL_OFFSET 0xC0000000

SECTIONS
{
    . = 0x00100000;
    _kernel_base = .;

    _boot_base = .;

    .boot_magic : {
        *(.boot_magic)
    }

    .boot_text : ALIGN(4K) {
        *(.boot_text)
    }

    .boot_bss : ALIGN(4K) {
        *(.boot_bss)
    }

    _boot_end = .;

    . += KERNEL_OFFSET;

    .text ALIGN(4K) : AT(ADDR(.text) - KERNEL_OFFSET) {
        _kernel_code_base = .;
        *(.text)
    }

    .rodata ALIGN(4K) : AT(ADDR(.rodata) - KERNEL_OFFSET) {
        *(.rodata)
        _kernel_code_end = .;
    }

    .data ALIGN(4K) : AT(ADDR(.data) - KERNEL_OFFSET) {
        _kernel_data_base = .;
        *(.data)
    }

    .bss ALIGN(4K) : AT(ADDR(.bss) - KERNEL_OFFSET) {
        *(COMMON)
        *(.bss)
        _kernel_data_end = .;
    }

    _kernel_end = .;
}